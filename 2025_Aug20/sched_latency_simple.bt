#!/usr/bin/env bpftrace

// Record timestamp when a task becomes runnable
tracepoint:sched:sched_wakeup {
   // Store the timestamp for this PID
   @wakeup[args->pid] = nsecs;
}

// Calculate delay when the task actually runs
tracepoint:sched:sched_switch {
   $pid = args->next_pid;
   $ts = @wakeup[$pid];
 
   // If we have a wakeup timestamp, calculate the delay
   if ($ts) {
      $delay = nsecs - $ts;
      // Only show delays > 1ms
      if ($delay > 1000000) {
          printf("%-16s %-6d %16llu ns\n", args->next_comm, $pid, $delay);
          @delay_us = hist($delay / 1000);
      }
      delete(@wakeup[$pid]);
    }
}

